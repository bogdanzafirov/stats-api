plugins {
  id 'java'
  id 'groovy'
  id 'org.springframework.boot' version '2.5.0'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id "org.openapi.generator" version "5.1.1"
  id 'org.unbroken-dome.test-sets' version '4.0.0'
}

group = 'com.solactive.stats'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
  mavenCentral()
}

dependencies {
  // Spring Web and JSON Problem dependencies
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.zalando:problem-spring-web-starter:0.26.2'

  // Lombok and Apache Collections dependencies
  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'
  implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'

  // Open API dependencies
  implementation 'io.swagger:swagger-annotations:1.6.2'
  implementation 'io.springfox:springfox-swagger2:2.9.2'
  implementation 'org.springframework.boot:spring-boot-starter-validation'

  // Evaluation dependencies
  implementation 'net.bull.javamelody:javamelody-spring-boot-starter:1.81.0'
  implementation 'com.thoughtworks.xstream:xstream:1.4.10'
  implementation 'org.jrobin:jrobin:1.5.9'

  // Spock testing
  testImplementation 'junit:junit:4.13.1'
  testImplementation 'org.codehaus.groovy:groovy:3.0.7'
  testImplementation 'org.spockframework:spock-core:2.0-M4-groovy-3.0'
  testImplementation 'org.spockframework:spock-spring:2.0-M4-groovy-3.0'

  // Test dependencies
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'com.statemachinesystems:mock-clock:1.0'

  testImplementation('org.springframework.boot:spring-boot-starter-test') {
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
}

openApiGenerate {
  generatorName = "spring"
  inputSpec = "$rootDir/src/main/resources/contract/statistics-api.yaml".toString()
  outputDir = "$buildDir/generated/sources/openapi".toString()
  modelPackage = "com.solactive.stats.generated.openapi.model"
  apiPackage = "com.solactive.stats.generated.openapi.api"
  configOptions = [
      interfaceOnly       : "true",
      dateLibrary         : "java8",
      useBeanValidation   : "true",
      library             : "spring-cloud",
      openApiNullable     : "false",
      skipDefaultInterface: "true"
  ]
}

compileJava.dependsOn tasks.openApiGenerate

sourceSets {
  main {
    java.srcDirs += "build/generated/sources/openapi/src/main/java"
  }
}

testSets {
  integrationTest {
    dirName = 'integration-test'
  }
}

check.dependsOn integrationTest

tasks.withType(Test) {
  // Use junit platform for unit and integration tests.
  useJUnitPlatform()
}
